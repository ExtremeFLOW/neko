module test_vector_registry
  use pfunit
  use vector, only : vector_t
  use vector_registry, only : vector_registry_t
  use neko_config, only : NEKO_BCKND_DEVICE
  use device, only : device_init, device_finalize
  use comm, only : NEKO_COMM, pe_rank, pe_size
  implicit none

  @TestCase
  type, extends(TestCase) :: test_vector_reg
   contains
     procedure :: setUp
     procedure :: tearDown
  end type test_vector_reg

contains

  subroutine setUp(this)
    class(test_vector_reg), intent(inout) :: this
    if (NEKO_BCKND_DEVICE .eq. 1) then
       call device_init
    end if
  end subroutine setUp

  subroutine tearDown(this)
    class(test_vector_reg), intent(inout) :: this
    if (NEKO_BCKND_DEVICE .eq. 1) then
       call device_finalize
    end if
  end subroutine tearDown

  @test
  subroutine test_vector_registry_init(this)
    class(test_vector_reg), intent(inout) :: this
    type(vector_registry_t) :: registry

    call registry%init()

    @assertEqual(registry%n_vectors(), 0)
    @assertEqual(registry%n_aliases(), 0)
    @assertEqual(registry%get_expansion_size(), 50)

    call registry%init(5, 10)
    @assertEqual(registry%n_vectors(), 0)
    @assertEqual(registry%n_aliases(), 0)
    @assertEqual(registry%get_expansion_size(), 10)
  end subroutine test_vector_registry_init

  @test
  subroutine test_vector_registry_add_vector(this)
    class(test_vector_reg), intent(inout) :: this
    type(vector_registry_t) :: registry
    type(vector_t), pointer :: vector_ptr

    call registry%init()

    call registry%add_vector(10 , 'test_vector')
    @assertEqual(registry%vector_exists('test_vector'), .true.)

    @assertEqual(registry%n_vectors(), 1)

    vector_ptr => registry%get_vector('test_vector')
    @assertEqual(vector_ptr%size(), 10)

    vector_ptr => registry%get_vector(1)
    @assertEqual(vector_ptr%size(), 10)

    call registry%add_vector(20 , 'test_vector', ignore_existing=.true.)
    @assertEqual(registry%n_vectors(), 1)
  end subroutine test_vector_registry_add_vector

  @test
  subroutine test_vector_registry_add_alias(this)
    class(test_vector_reg), intent(inout) :: this
    type(vector_registry_t) :: registry
    type(vector_t), pointer :: vector_ptr

    call registry%init()

    call registry%add_vector(10 , 'test_vector')
    call registry%add_alias('test_alias', 'test_vector')
    call registry%add_alias('test_alias2', 'test_vector')

    @assertEqual(registry%vector_exists('test_vector'), .true.)
    @assertEqual(registry%vector_exists('test_alias'), .true.)
    @assertEqual(registry%vector_exists('test_alias2'), .true.)

    @assertEqual(registry%n_aliases(), 2)

    vector_ptr => registry%get_vector('test_alias')
    @assertEqual(vector_ptr%size(), 10)

    vector_ptr => registry%get_vector('test_alias2')
    @assertEqual(vector_ptr%size(), 10)
  end subroutine test_vector_registry_add_alias

  @test
  subroutine test_vector_registry_expand(this)
    class(test_vector_reg), intent(inout) :: this
    type(vector_registry_t) :: registry
    type(vector_t), pointer :: vector_ptr

    call registry%init(1, 1)

    call registry%add_vector(10 , 'test_vector1')
    call registry%add_vector(20 , 'test_vector2')

    @assertEqual(registry%n_vectors(), 2)
  end subroutine test_vector_registry_expand


end module test_vector_registry
