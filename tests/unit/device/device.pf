
@test
subroutine test_device_init
  use pfunit
  use device
  implicit none
  call device_init()
end subroutine test_device_init

@test
subroutine test_device_sync
  use pfunit
  use device
  implicit none

  call device_init()
  call device_sync()

end subroutine test_device_sync

@test
subroutine test_device_alloc
  use neko_config
  use pfunit
  use device
  implicit none
  type(c_ptr) :: x_d
  integer(c_size_t) :: size_

  if (NEKO_BCKND_DEVICE .eq. 1) then
     size_ = 42 * 4
     x_d = C_NULL_PTR

     call device_alloc(x_d, size_)

     @assertTrue(c_associated(x_d))

  end if

end subroutine test_device_alloc

@test
subroutine test_device_free
  use neko_config
  use pfunit
  use device
  implicit none
  type(c_ptr) :: x_d
  integer(c_size_t) :: size_

  if (NEKO_BCKND_DEVICE .eq. 1) then

     size_ = 42 * 4
     x_d = C_NULL_PTR

     call device_alloc(x_d, size_)

     @assertTrue(c_associated(x_d))

     call device_free(x_d)

     @assertFalse(c_associated(x_d))

  end if

end subroutine test_device_free

@test
subroutine test_device_associate
  use neko_config
  use pfunit
  use device
  implicit none
  type(c_ptr) :: x_d
  integer(c_size_t) :: size_
  integer :: x(42)

  if (NEKO_BCKND_DEVICE .eq. 1) then

     size_ = size(x) * 4
     x_d = C_NULL_PTR

     call device_alloc(x_d, size_)
     call device_associate(x, x_d)

     @assertTrue(c_associated(x_d))

  end if

end subroutine test_device_associate

@test
subroutine test_device_associated
  use neko_config
  use pfunit
  use device
  implicit none
  type(c_ptr) :: x_d
  integer(c_size_t) :: size_
  integer :: x(42)

  if (NEKO_BCKND_DEVICE .eq. 1) then

     size_ = size(x) * 4
     x_d = C_NULL_PTR

     call device_alloc(x_d, size_)
     call device_associate(x, x_d)

     @assertTrue(device_associated(x))

  end if

end subroutine test_device_associated

@test
subroutine test_device_get_ptr
  use neko_config
  use pfunit
  use device
  implicit none
  type(c_ptr) :: x_d, dev
  integer(c_size_t) :: size_
  integer :: x(42)

  if (NEKO_BCKND_DEVICE .eq. 1) then

     size_ = size(x) * 4

     x_d = C_NULL_PTR
     dev = C_NULL_PTR

     call device_alloc(x_d, size_)
     call device_associate(x, x_d)

     dev = device_get_ptr(x)

     @assertTrue(c_associated(x_d, dev))

  end if


end subroutine test_device_get_ptr

@test
subroutine test_device_map
  use neko_config
  use pfunit
  use device
  implicit none
  type(c_ptr) :: x_d, dev
  integer(c_size_t) :: size_
  integer :: x(42)

  if (NEKO_BCKND_DEVICE .eq. 1) then

     size_ = size(x) * 4
     x_d = C_NULL_PTR
     dev = C_NULL_PTR

     call device_map(x, x_d, size(x))

     dev = device_get_ptr(x)

     @assertTrue(c_associated(x_d, dev))

  end if


end subroutine test_device_map

@test
subroutine test_device_memcpy
  use neko_config
  use pfunit
  use device
  implicit none
  type(c_ptr) :: x_d, y_d, dev
  integer(c_size_t) :: size_
  integer :: x(42), i

  if (NEKO_BCKND_DEVICE .eq. 1) then

     size_ = size(x) * 4
     x_d = C_NULL_PTR
     y_d = C_NULL_PTR
     dev = C_NULL_PTR

     call device_map(x, x_d, size(x))

     dev = device_get_ptr(x)

     do i = 1, size(x)
        x(i) = i
     end do

     call device_memcpy(x, x_d, size(X), HOST_TO_DEVICE, sync=.false.)

     x = 0

     call device_memcpy(x, x_d, size(x), DEVICE_TO_HOST, sync=.true.)

     do i = 1, size(x)
        @assertEqual(x(i), i)
     end do

     call device_alloc(y_d, size_)

     call device_memcpy(y_d, x_d, size_, DEVICE_TO_DEVICE)

     x = 0

     call device_memcpy(x, y_d, size(x), DEVICE_TO_HOST, sync=.true.)

     do i = 1, size(x)
        @assertEqual(x(i), i)
     end do

  end if

end subroutine test_device_memcpy

@test
subroutine test_device_stream_create
  use neko_config
  use pfunit
  use device
  implicit none
  type(c_ptr) :: stream

  if ((NEKO_BCKND_HIP .eq. 1) .or. (NEKO_BCKND_CUDA .eq. 1)) then

     stream = C_NULL_PTR
     call device_stream_create(stream)
     @assertTrue(c_associated(stream))

  end if

end subroutine test_device_stream_create

@test
subroutine test_device_stream_destroy
  use neko_config
  use pfunit
  use device
  implicit none
  type(c_ptr) :: stream

  if ((NEKO_BCKND_HIP .eq. 1) .or. (NEKO_BCKND_CUDA .eq. 1)) then

     stream = C_NULL_PTR
     call device_stream_create(stream)
     @assertTrue(c_associated(stream))

     call device_stream_destroy(stream)

  end if

end subroutine test_device_stream_destroy

@test
subroutine test_device_sync_stream
  use neko_config
  use pfunit
  use device
  implicit none
  type(c_ptr) :: stream1, stream2

  if ((NEKO_BCKND_HIP .eq. 1) .or. (NEKO_BCKND_CUDA .eq. 1)) then

     stream1 = C_NULL_PTR
     stream2 = C_NULL_PTR
     call device_stream_create(stream1)
     call device_stream_create(stream2)
     @assertTrue(c_associated(stream1))
     @assertTrue(c_associated(stream2))

     call device_sync(stream1)
     call device_sync(stream2)
     call device_sync(stream2)
     call device_sync(stream1)

     call device_stream_destroy(stream1)
     call device_stream_destroy(stream2)

  end if

end subroutine test_device_sync_stream
